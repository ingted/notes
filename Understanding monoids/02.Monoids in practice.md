# 実践モノイド(翻訳)
難しくないモノイド パート2

原文：[Monoids in practice][link01]

----

[前回][link02] の記事ではモノイドの定義について説明しました。
今回は一部のモノイドの実装方法について説明します。

まず定義を再度確認しておきます：

* 一連のものがあって、これらのうち2つを一度に連結するような方法がある
* 規則1 (クロージャ)：2つのものを連結した結果は常に別のものになる
* 規則2 (結合性)：2つ以上のものを連結する場合、最初に連結する対はどれでも構わない
* 規則3 (単位元)：別のものと連結しても常に別のものが返されるような「ゼロ」と呼ばれる特別なものがある

たとえばあるものが文字列だとして、演算子が連結だとするとこれらはモノイドです。
具体的なコードとしては以下のようになります：

```fsharp
let s1 = "hello"
let s2 = " world!"

// クロージャ
let sum = s1 + s2 // sumは文字列

// 結合性
let s3 = "x"
let s4a = (s1+s2) + s3
let s4b = s1 + (s2+s3)
assert (s4a = s4b)

// 空文字列が単位元
assert (s1 + "" = s1)
assert ("" + s1 = s1)
```

しかし今回はもう少し複雑なオブジェクトに応用してみます。

たとえば販売注文の1品目を表す次のような小さな構造体``OrderLine``があるとします。

```fsharp
type OrderLine = {
    ProductCode: string
    Qty: int
    Total: float
    }
```

そしておそらくは1つの注文に対する総額を計算したいはずで、
その場合には注文品目のリストにある ``Total`` の総和を計算することになります。

普通に命令的アプローチで実装する場合、
以下のように ``total`` というローカル変数を用意しておいて
各品目をループしつつ足していくことになるでしょう：

```fsharp
let calculateOrderTotal lines =
    let mutable total = 0.0
    for line in lines do
        total <- total + line.Total
    total
```

試してみましょう：

```fsharp
module OrdersUsingImperativeLoop =

    type OrderLine = {
        ProductCode: string
        Qty: int
        Total: float
        }

    let calculateOrderTotal lines =
        let mutable total = 0.0
        for line in lines do
            total <- total + line.Total
        total

    let orderLines = [
        { ProductCode="AAA"; Qty=2; Total=19.98 }
        { ProductCode="BBB"; Qty=1; Total=1.99 }
        { ProductCode="CCC"; Qty=3; Total=3.99 }
        ]

    orderLines
    |> calculateOrderTotal
    |> printfn "総額は %g"
```

しかしもちろん熟練の関数プログラマであれば
このコードをあざ笑いつつ、以下のように
``fold`` を使って ``calculateOrderTotal`` を書き換えるでしょう：

```fsharp
module OrdersUsingImperativeLoop =

    type OrderLine = {
        ProductCode: string
        Qty: int
        Total: float
        }

    let calculateOrderTotal lines =
        let accumulateTotal total line =
            total + line.Total
        lines
        |> List.fold accumulateTotal 0.0

    let orderLines = [
        { ProductCode="AAA"; Qty=2; Total=19.98 }
        { ProductCode="BBB"; Qty=1; Total=1.99 }
        { ProductCode="CCC"; Qty=3; Total=3.99 }
        ]

    orderLines
    |> calculateOrderTotal
    |> printfn "総額は %g"
```

ここまでは順調です。
ではモノイドアプローチを使った解法を見てみましょう。

モノイドの場合、加算あるいは連結演算子のようなものを定義する必要があります。
こんな感じでどうでしょう？

```fsharp
let addLine orderLine1 orderLine2 =
    orderLine1.Total + orderLine2.Total
```

しかしこれではモノイドの重要な性質が損なわれてしまっているので駄目です。
加算は必ず同じ型を返さなければいけません！

``addLine`` 関数のシグネチャは以下のようになっています：

```fsharp
addLine : OrderLine -> OrderLine -> float
```

このように、 ``OrderLine`` ではなく ``float`` が返り値の型になっています。

つまり全く別の ``OrderLine`` を返すようにする必要があります。
正しい実装は以下の通りです：

```fsharp
let addLine orderLine1 orderLine2 =
    {
    ProductCode = "TOTAL"
    Qty = orderLine1.Qty + orderLine2.Qty
    Total = orderLine1.Total + orderLine2.Total
    }
```

これでシグネチャも正しくなりました：``addLine : OrderLine -> OrderLine -> OrderLine``

なお完全な構造体を返す必要があるため、 ``Total`` だけではなく
``ProductCode`` や ``Qty`` についても何かしら値を
設定する必要がある点に注意してください。
``Qty`` は単に和を設定すれば良いので簡単です。
``ProductCode`` は今のところ実在する製品コードを使えるわけではないので
とりあえず「TOTAL」としておきました。

簡単なテストをしてみましょう：

```fsharp
// OrderLineを出力するユーティリティメソッド
let printLine { ProductCode=p; Qty=q; Total=t } =
    printfn "%-10s %5i %6g" p q t

let orderLine1 = { ProductCode="AAA"; Qty=2; Total=19.98 }
let orderLine2 = { ProductCode="BBB"; Qty=1; Total=1.99 }

// 3つめのものを作るために2つを加算
let orderLine3 = addLine orderLine1 orderLine2
orderLine3 |> printLine // そしてそれを出力
```

結果は以下のようになります：

```
TOTAL          3  21.97
```

> 原注：printfの書式オプションの詳細については [こちらのprintfに関する記事][link03] を参照してください。

では ``reduce`` を使ってこの関数をリストに適用してみましょう：

```fsharp
let orderLines = [
    { ProductCode="AAA"; Qty=2; Total=19.98 }
    { ProductCode="BBB"; Qty=1; Total=1.99 }
    { ProductCode="CCC"; Qty=3; Total=3.99 }
    ]

orderLines
|> List.reduce addLine
|> printLine
```

結果は以下の通りです：

```
TOTAL          6  25.96
```

最初のうちはこれが余計な処理をしていて、総額を計算しているように見えるかもしれません。
しかし実際のところは単に総額だけではなく、それ以上の情報、つまり総量についても
計算できているという点に注意してください。

たとえば ``printLine`` 関数を再利用して、
以下のような総額も含んだレシート印刷用の関数を作ることもできます：

```fsharp
let printReceipt lines =
    lines
    |> List.iter printLine

    printfn "-----------------------"

    lines
    |> List.reduce addLine
    |> printLine

orderLines
|> printReceipt
```

出力結果は以下のようになります：

```
AAA            2  19.98
BBB            1   1.99
CCC            3   3.99
-----------------------
TOTAL          6  25.96
```

さらに重要な点として、モノイドのインクリメント可能な性質を利用することにより、
新しい品目を追加する度に小計を更新することができます。

たとえば以下のような場合です：

```fsharp
let subtotal = orderLines |> List.reduce addLine
let newLine = { ProductCode="DDD"; Qty=1; Total=29.98 }
let newSubtotal = subtotal |> addLine newLine
newSubtotal |> printLine
```

各品目をあたかも数値と同じように足しあわせることができるように、
独自の演算子 ``++`` を定義することもできます：

```fsharp
let (++) a b = addLine a b

let newSubtotal = subtotal ++ newLine
```

このように、モノイドパターンを使うと全く新しい考え方が出来るようになります。
今回のような「足し算」のアプローチはほとんどすべてのオブジェクトに適用できます。

たとえば製品「足す」製品はどうなるでしょう？
あるいは顧客「足す」顧客は？
想像力を自由に働かせてみてください！

## もう終わり？

皆さんはきっとまだ全然説明が終わってないと思っていることでしょう。
モノイドの3つめの条件、ゼロあるいは単位元についての説明がまだ残っています。

今回の場合、注文品目に追加をしても元の注文品目が返されるような
``OrderLine`` が必要です。
有りますか？

今のところはありません。
というのも、加算演算子は常に製品コードを「TOTAL」にしてしまうからです。
したがってこれはモノイドではなくて **半群** です。

既に説明したように、半群も十分有用です。
しかし空の注文品目に対する総額を計算しようとすると問題が起こります。
どのような結果になるべきでしょう？

1つの回避策としては、 ``addLine`` 関数を変更して空の製品コードを
無視するようにする方法です。
そうすれば空の製品コードを持った注文品目がゼロ要素として機能するようになります。

つまりこういうことです：

```fsharp
let addLine orderLine1 orderLine2 =
    match orderLine1.ProductCode, orderLine2.ProductCode with
    // どちらかがゼロ？そうであれば他方を返します
    | "", _ -> orderLine2
    | _, "" -> orderLine1
    // その他の場合は以前と同じです
    | _ ->
        {
        ProductCode = "TOTAL"
        Qty = orderLine1.Qty + orderLine2.Qty
        Total = orderLine1.Total + orderLine2.Total
        }

let zero = { ProductCode=""; Qty=0; Total=0.0 }
let orderLine1 = { ProductCode="AAA"; Qty=2; Total=19.98 }
```

この単位元が正しく機能しているかどうかテストしてみましょう：

```fsharp
assert (orderLine1 = addLine orderLine1 zero)
assert (orderLine1 = addLine zero orderLine1)
```

この方法は少し場当たり的なので、一般的にこのテクニックを採用することはおすすめしません。
後ほど説明しますが、単位元を作る別の方法もあります。

## 特別な総額用の型を用意する

以上の例にある ``OrderLine`` 型は非常に単純で、総額のフィールドを簡単に上書きできます。

しかし ``OrderLine`` 型がもっと複雑だった場合はどうでしょう？
たとえば以下のように ``Price`` フィールドを含んでいたとします：

```fsharp
type OrderLine = {
    ProductCode: string
    Qty: int
    Price: float
    Total: float
    }
```

さてそうすると面倒なことになります。
2つの品目を足しあわせるときに ``Price`` には何を設定すればいいでしょう？
価格の平均値？
あるいは価格無し？

```fsharp
let addLine orderLine1 orderLine2 =
    {
    ProductCode = "TOTAL"
    Qty = orderLine1.Qty + orderLine2.Qty
    Price = 0 // あるいは平均価格？
    Total = orderLine1.Total + orderLine2.Total
    }
```

どちらも正しくなさそうです。
どうすべきかわからないということは、おそらく設計が間違っているということなのでしょう。

実際、合計を出すだけであればすべてのデータではなく一部のデータだけで十分です。
これをどうやって表現しましょう？

もちろん判別共用体ですね！
1つは製品品目、そしてもう1つは合計だけを表すようにします。

つまりこういうことです：

```fsharp
type ProductLine = {
    ProductCode: string
    Qty: int
    Price: float
    LineTotal: float
    }

type TotalLine = {
    Qty: int
    OrderTotal: float
    }

type OrderLine =
    | Product of ProductLine
    | Total of TotalLine
```

こちらの設計のほうがだいぶいい感じです。
今回は合計用の特別な構造体も追加したので、
データを無理矢理調整する必要もなくなっています。
また、「TOTAL」というダミーの製品コードも不要です。

> 各レコードの「合計」フィールドにはそれぞれ別の名前をつけている点に注意してください。
> このように別の名前をつけておけば、常に明示的に型を指定する必要がなくなります。

それぞれの判別子に応じて処理を行わないといけなくなったので、
残念ながら足し算のロジックはかなり複雑になります：

```fsharp
let addLine orderLine1 orderLine2 =
    let totalLine =
        match orderLine1, orderLine2 with
        | Product p1, Product p2 ->
            { Qty = p1.Qty + p2.Qty
              OrderTotal = p1.LineTotal + p2.LineTotal }
        | Product p, Total t ->
            { Qty = p.Qty + t.Qty
              OrderTotal = p.LineTotal + t.OrderTotal }
        | Total t, Product p ->
            { Qty = p.Qty + t.Qty
              OrderTotal = p.LineTotal + t.OrderTotal }
        | Total t1, Total t2 ->
            { Qty = t1.Qty + t2.Qty
              OrderTotal = t1.OrderTotal + t2.OrderTotal }
    Total totalLine // totalLineがOrderLineになるようにラップします
```

なお ``TotalLine`` の値をそのまま返すことができない点に注意してください。
ここでは ``Total`` ケースを使って正しく ``OrderLine`` になるようにしています。
もしこれを省略してしまうと ``addLine`` のシグネチャが
``OrderLine -> OrderLine -> TotalLine`` と間違った形になってしまいます。
``OrderLine -> OrderLine -> OrderLine`` というシグネチャが必要です。
それ以外は駄目です！

さて2つのケースに分かれるようになったため、
``printLine`` 関数もそれぞれに対応するよう変更する必要があります：

```fsharp
let printLine = function
    | Product { ProductCode=p; Qty=q; Price=pr; LineTotal=t } ->
        printfn "%-10s %5i 単価 %4g 合計 %6g" p q pr t
    | Total { Qty=q; OrderTotal=t } ->
        printfn "%-10s %5i                %6g" "TOTAL" q t
```

ここまで来れば以前と同じように足せるようになります：

```fsharp
let orderLine1 = Product { ProductCode="AAA"; Qty=2; Price=9.99; LineTotal=19.98 }
let orderLine2 = Product { ProductCode="BBB"; Qty=1; Price=1.99; LineTotal=1.99 }
let orderLine3 = addLine orderLine1 orderLine2

orderLine1 |> printLine
orderLine2 |> printLine
orderLine3 |> printLine
```

## 再び単位元

今回もまだ単位元について説明していません。
以前と同じく、空の製品コードを使えばよさそうですが、
そうすると ``Product`` ケースだけにしか機能しません。

正しい単位元を用意するためには、 **3** 番目のケースとして
``EmptyOrder`` を追加する必要があります。

```fsharp
type ProductLine = {
    ProductCode: string
    Qty: int
    Price: float
    LineTotal: float
    }

type TotalLine = {
    Qty: int
    OrderTotal: float
    }

type OrderLine =
    | Product of ProductLine
    | Total of TotalLine
    | EmptyOrder
```

そしてこの新しく追加したケースが処理されるように ``addLine`` 関数を書き換えます：

```fsharp
let addLine orderLine1 orderLine2 =
    match orderLine1, orderLine2 with
    // どちらかがゼロ？その場合は他方を返す
    | EmptyOrder, _ -> orderLine2
    | _, EmptyOrder -> orderLine1
    // その他については以前と同じ
    | Product p1, Product p2 ->
        Total { Qty = p1.Qty + p2.Qty;
          OrderTotal = p1.LineTotal + p2.LineTotal }
    | Product p, Total t ->
        Total { Qty = p.Qty + t.Qty;
          OrderTotal = p.LineTotal + t.OrderTotal }
    | Total t, Product p ->
        Total { Qty = p.Qty + t.Qty;
          OrderTotal = p.LineTotal + t.OrderTotal }
    | Total t1, Total t2 ->
        Total { Qty = t1.Qty + t2.Qty;
          OrderTotal = t1.OrderTotal + t2.OrderTotal }
```

以下のようにテストしてみます：

```fsharp
let zero = EmptyOrder

// 単位元のテスト
let productLine = Product { ProductCode="AAA"; Qty=2; Price=9.99; LineTotal=19.98 }
assert ( productLine = addLine productLine zero )
assert ( productLine = addLine zero productLine )

let totalLine = Total { Qty=2; OrderTotal=19.98 }
assert ( totalLine = addLine totalLine zero )
assert ( totalLine = addLine zero totalLine )
```

## 組み込みのList.sum関数を使う

実のところ、 ``List.sum`` 関数はモノイドのことを知っていたのです！
この関数に足し算の演算子が何で、ゼロが何かということを教えてあげると
``List.fold`` の代わりに ``List.sum`` を直接使うことができます。

そこで以下のような ``+`` と ``Zero`` という2つの静的メンバを
型に追加します：

```fsharp
type OrderLine with
    static member (+) (x,y) = addLine x y
    static member Zero = EmptyOrder
```

そうすると ``List.sum`` が期待通りに動作するようになります：

```fsharp
let lines1 = [productLine]
// 明示的に演算子とゼロを指定してfoldを呼ぶ
lines1 |> List.fold addLine zero |> printfn "%A"
// 暗黙的に演算子とゼロを使ってmapを呼ぶ
lines1 |> List.sum |> printfn "%A"

let emptyList: OrderLine list = []
// 明示的に演算子とゼロを指定してfoldを呼ぶ
emptyList |> List.fold addLine zero |> printfn "%A"
// 暗黙的に演算子とゼロを使ってmapを呼ぶ
emptyList |> List.sum |> printfn "%A"
```

なおこのコードが正しく動作するためには、 ``Zero`` という名前の
既存のメソッドやケースが存在してはいけません。
``EmptyOrder`` の代わりに ``Zero`` という名前にしていたら
動作しなかったでしょう。

分かりづらい話ではありますが、たとえば ``ComplexNumber`` や ``Vector`` のような
数学に関連する型を作成していない限り、この名前が採用されることはないと思います。
もし採用していたのであれば、筆者の感覚的には少しやり過ぎな感じがします。

さてこのトリックを実現する場合、 ``Zero`` メンバは
拡張メソッドにはできない点に注意してください。
必ず型のメンバとして定義する必要があります。

たとえば以下のコードでは文字列用の「ゼロ」を定義しようとしています。

``List.fold`` には拡張メソッドとして定義された ``String.Zero`` が見えているので
正しく機能しますが、 ``List.sum`` からは拡張メソッドが見えないので
エラーになります。

```fsharp
module StringMonoid =

    // 拡張メソッドを定義
    type System.String with
        static member Zero = ""

    // OK
    ["a"; "b"; "c"]
    |> List.reduce (+)
    |> printfn "reduceの場合：%s"

    // OK。拡張メソッドであるString.Zeroは見えている
    ["a"; "b"; "c"]
    |> List.fold (+) System.String.Zero
    |> printfn "foldの場合：%s"

    // エラー。List.sumからはString.Zeroが見えない
    ["a"; "b"; "c"]
    |> List.sum
    |> printfn "sumの場合：%s"
```

## 別の構造体にマッピングする

判別共用体として2つのケースを用意したおかげで
注文品目用のケースを持てるようになったわけですが、
現実的なコードの場合、この方法はあまりに複雑すぎるか、
あるいは紛らわしいものです。

以下のような顧客レコードがあるとします：

```fsharp
open System

type Customer = {
    Name: string // および多数の文字列フィールド！
    LastActive: DateTime
    TotalSpend: float }
```

どうやって二人の顧客を「足し算」したらいいでしょうか？

目安としては、集計機能は実際のところ数字あるいは数字的な型に対してしか
機能しないという点に注目するとよいでしょう。
文字列は簡単には集計できません。

したがって ``Customer`` を集計するのではなく、
すべての集計情報を含むような ``CustomerStats`` を別に定義するとよいでしょう：

```fsharp
// 顧客の統計情報を追跡するための型を作成する
type CustomerStats = {
    // 以下の統計情報に含まれる顧客の数
    Count: int
    // 最後の利用日からの経過日数の合計
    TotalInactiveDays: int
    // 総額
    TotalSpend: float }
```

``CustomerStats`` のフィールドはどれも数値なので、
2つの統計情報を簡単に足し算できます：

```fsharp
let add stat1 stat2 = {
    Count = stat1.Count + stat2.Count
    TotalInactiveDays = stat1.TotalInactiveDays + stat2.TotalInactiveDays
    TotalSpend = stat1.TotalSpend + stat2.TotalSpend
    }

// 中間演算子バージョンも定義する
let (++) a b = add a b
```

これまでと同じく、 ``add`` 関数の入力と出力は同じ型でなければいけません。
つまり ``Customer -> Customer -> CustomerStats`` などではなく、
``CustomerStats -> CustomerStats -> CustomerStats`` でなければいけないのです。

OK。ここまではいいでしょう。

次は顧客のコレクションがあって、そこから統計情報を集計するには
どうしたらいいでしょうか？

顧客をそのまま足しあわせることはできないので、
まずはそれぞれの顧客を ``Customerstats`` に変換して、
その後にモノイド演算子を使って統計情報を足していくことになります。

たとえば以下のようになります：

```fsharp
// 顧客を統計情報に変換
let toStats cust =
    let inactiveDays = DateTime.Now.Subtract(cust.LastActive).Days;
    { Count=1; TotalInactiveDays=inactiveDays; TotalSpend=cust.TotalSpend }

// 顧客のリストを作成
let c1 = { Name="Alice"; LastActive=DateTime(2005,1,1); TotalSpend=100.0 }
let c2 = { Name="Bob"; LastActive=DateTime(2010,2,2); TotalSpend=45.0 }
let c3 = { Name="Charlie"; LastActive=DateTime(2011,3,3); TotalSpend=42.0 }
let customers = [c1;c2;c3]

// 統計情報を集計
customers
|> List.map toStats
|> List.reduce add
|> printfn "結果 = %A"
```

まず、 ``toStats`` は1人の顧客に対する統計情報しか作成しないことに注意してください。
そのためカウントを1に設定しています。
これは不思議に思えるかもしれませんがこれでいいのです。
リストに1人しか顧客が含まれなければ、集計後の統計情報には
まさにその1人しか含まれないわけです。

また、集計後の結果にも注意してください。
まず ``map`` を使って元の型をモノイド型に変換した後、
``reduce`` を使ってすべての統計情報を集計しています。

んー、、、``map`` した後に ``reduce``。
何か思い出しませんか？

そうです。かの有名なGoogleのMapReduceアルゴリズムは
まさにこのコンセプトに沿ったものなのです
(細かい部分ではやや違うものですが)。

説明を続ける前に理解力テストを出題することにしましょう。

* ``CustomerStats`` にとっての「ゼロ」は何でしょう？
  空のリストに対して ``List.fold`` を使ってコードをテストしてみましょう。
* 単純な ``OrderStats`` クラスを作成して、このクラスを使って
  今回の記事の先頭で定義した ``OrderLine`` 型を集計してみましょう。

## モノイド準同型

これまでの説明により、 **モノイド準同型** (Monoid Homomorphisms)
というものを理解するための用意ができました。

皆さんが何を考えているかわかりますとも。
あーもう！
わけのわからない数学用語を一度に2つも出さないでくれ！

ですが今はもう「モノイド」という単語は怖いものではなくなったのではないでしょうか。
また、「準同型」という数学用語も、聞いた感じよりは単純なものです。
単に「形が同じ」であって、同じ「形」を維持するような
マッピングあるいは関数を説明するものでしかありません。

具体的にはどういうことでしょう？

これまでも、モノイドにはある特定の共通した構造があるという説明をしてきました。
つまり根底をなすオブジェクト(数値や文字列、リスト、 ``CustomerStats`` など)が
全く異なっていたとしても、それぞれの「モノイド性」は変わりません。
かつてのGeorge W. Bushの言葉を応用すると、
ひとたびモノイドがわかればすべてが分かったも同然です。

要するに **モノイド** 準同型とはある変換を表すもので、
たとえ「変換前」と「変換後」のオブジェクトが全く別物であったとしても
本質的な「モノイド性」が損なわれないような変換のことです。

この節では単純なモノイド準同型を紹介しましょう。
「hello world」や「fibonacci series」などに対して
単語数を数えるようなモノイド準同型です。

### モノイドとしてのドキュメント

たとえば以下のようなテキストブロックを表す型があるとしましょう：

```fsharp
type Text = Text of string
```

そして以下のようにすると2つの小さなテキストブロックを足して
大きなテキストブロックにすることができます：

```fsharp
let addText (Text s1) (Text s2) =
    Text (s1 + s2)
```

足し算の動作は以下のようにしてテストできます：

```fsharp
let t1 = Text "Hello"
let t2 = Text " World"
let t3 = addText t1 t2
```

皆さんはもうエキスパートになったわけなので、
これがモノイドであることがすぐに分かり、
ゼロも ``Text ""`` であることが分かるでしょう。

では( [こちらのような][link04] )書籍を執筆していて、
原稿にどのくらいの量の単語を書いたのか知りたいとしましょう。

以下はかなり大雑把な実装とそのテストコードです：

```fsharp
let wordCount (Text s) =
    s.Split(' ').Length

// テスト
Text "Hello world"
|> wordCount
|> printfn "単語数は %i"
```

さて執筆を続けてテキストが3ページ分出来上がったとしましょう。
ドキュメント全体の単語数はどうやって計算したらいいでしょうか？

たとえばそれぞれバラバラなページを1つの完全なテキストブロックとして
足しあわせた後に、このテキストブロックに対して ``wordCount`` を
適用できるでしょう。
次のようなダイアグラムになります：

![ページを組み合わせた後にカウント][img01]

しかしこれだと新しいページが出来上がる度にすべてのテキストを足して
全体の単語数をカウントすることになります。

言うまでもなく、もう少しいい方法があることにお気づきでしょう。
すべてのテキストをまとめてからカウントするのではなく、
以下のようにそれぞれのページ毎にカウントして
それらを最終的に足しあわせればよいのです：

![ページ毎にカウントして合計する][img02]

2番目の方法は整数(カウント)自体がモノイドであって、
それぞれを足しあわせれば期待した通りの結果が得られるということに依存しています。

つまり ``wordCount`` 関数は「ページ」を集計するのではなく、
「単語数」を集計するようになります。

しかし大きな問題があります。
``wordCount`` はモノイド準同型なのでしょうか？

ページ(テキスト)と単語数(整数)はいずれもモノイドなので、
確かに ``wordCount`` はモノイドを別のモノイドに変換しています。

しかしもっと潜在的な条件、つまり「形」の維持についてはどうでしょうか？
つまり、単語数の足し算はページの足し算と同じ結果になりますか？

今回の場合はyesです。
したがって ``wordCount`` はモノイド準同型です！

これは当然であって、今回のようなマッピングがすべて
モノイド準同型になるだろうと思うかもしれません。
しかしそうならないケースについて後ほど紹介します。

### チャンク化できることの利点

モノイド準同型のアプローチによる利点は「チャンク化できること」(chunkable)にあります。

> 訳注：チャンクとはある程度の大きさをもった塊のことです。

各マップと単語数はそれぞれ独立したものです。
そのため個別に処理した後に最終的な答えを後で足しあわせることができるわけです。
多くのアルゴリズムにとっては、大きなチャンクを対象にするよりは
小さなチャンクを対象にしたほうが効率的です。
したがって可能な限りチャンク化するようにしたほうがよいでしょう。

チャンク化できることによって得られる直接的な結果としては、
先の記事で触れたようないくつかの恩恵が得られるようになります。

まずインクリメンタル(incremental)な処理が可能になります。
つまり最後のページにテキストを追加したとしても、
それ以前のすべてのページにある単語数を再計算する必要がありません。
したがって処理時間がいくらか短縮できます。

次に並列化が可能になります(parallelizable)。
それぞれのチャンクは別のコアや別のマシン上で独立して処理できます。
ただし現実的には並列化は過大評価されているところがあります。
小さく細分化できることによって得られる利点は並列化というよりは
パフォーマンス的に有利になるという方が大きいでしょう。

### 単語数カウントの実装を比較する

さてでは2つの異なるテクニックを使ってコードを書いてみましょう。

まずは上で紹介したような基本的な実装から始めます。
ただし単語数をカウントする部分については ``Split`` の代わりに
正規表現を使用するように変更しています。

```fsharp
module WordCountTest =
    open System

    type Text = Text of string

    let addText (Text s1) (Text s2) =
        Text (s1 + s2)

    let wordCount (Text s) =
        System.Text.RegularExpressions.Regex.Matches(s,@"\S+").Count
```

次に1000個の単語を含んだページを1000ページ持ったドキュメントを用意します。

```fsharp
module WordCountTest =

    // 前述のコード

    let page() =
        List.replicate 1000 "hello "
        |> List.reduce (+)
        |> Text

    let document() =
        page() |> List.replicate 1000
```

また、それぞれの実装による違いを計測するようなコードも必要になります。
小さなヘルパー関数を用意しましょう。

```fsharp
module WordCountTest =

    // 前述のコード

    let time f msg =
        let stopwatch = Diagnostics.Stopwatch()
        stopwatch.Start()
        f()
        stopwatch.Stop()
        printfn "%s にかかった時間は %ims です" msg stopwatch.ElapsedMilliseconds
```

では1番目のアプローチを実装しましょう。
すべてのページを ``addText`` でまとめた後に、
100万の単語を含んだドキュメント全体の単語数を計算します。

```fsharp
module WordCountTest =

    // 前述のコード

    let wordCountViaAddText() =
        document()
        |> List.reduce addText
        |> wordCount
        |> printfn "単語数は %i"

    time wordCountViaAddText "reduce後にカウント"
```

2番目のアプローチではまず各ページに対して ``wordCount`` を呼び出した後、
すべての結果を足しあわせます(当然 ``reduce`` を使用します)。

```fsharp
module WordCountTest =

    // 前述のコード

    let wordCountViaMap() =
        document()
        |> List.map wordCount
        |> List.reduce (+)
        |> printfn "単語数は %i"

    time wordCountViaMap "map後にreduce"
```

わずか2行しかコードを変更していない点に注目してください！

``wordCountViaAddText`` では以下のようにしてました：

```fsharp
|> List.reduce addText
|> wordCount
```

そして ``wordCountViaMap`` ではこの順番を入れ替えています。
以下の通り、 **最初に** ``wordCount`` を呼び出してから ``reduce`` を呼び出しています：

```fsharp
|> List.map wordCount
|> List.reduce (+)
```

最後に並列化による違いを確認してみましょう。
``List.map`` の代わりに組み込みの ``Array.Parallel.map`` を使います。
つまり最初にリストを配列に変換する必要があります。

```fsharp
module WordCountTest =

    // 前述のコード

    let wordCountViaParallelAddCounts() =
        document()
        |> List.toArray
        |> Array.Parallel.map wordCount
        |> Array.reduce (+)
        |> printfn "単語数は %i"

    time wordCountViaParallelAddCounts "並列map後にreduce"
```

これらの実装について、皆さんがきちんとついていけているとよいのですがどうでしょうか。

### 結果の分析

筆者の4コアマシン上で実行した結果は以下の通りです：

```
reduce後にカウント にかかった時間は 7955ms です
map後にreduce にかかった時間は 698ms です
並列map後にreduce にかかった時間は 603ms です
```

この結果は大まかなもので、きちんとしたパフォーマンスプロファイル
ではない点に注意してください。
ただしそれでもmap/reduceバージョンの方が ``ViaAddText`` バージョンよりも
明らかに性能がいいことがわかるでしょう。

これがまさにモノイド準同型が重要である理由なのです。
モノイド準同型があることによって、強力かつ簡単に実装できる
「分割統治」アルゴリズムが利用できるようになるのです。

もちろんこのアルゴリズムが効果的ではないようなケースもあるでしょう。
巨大なテキストブロックを文字列連結で処理する方法は全く効率的では無く、
単語の数え方ももっと適切な方法があるでしょう。
しかしそれでも基本的な部分は変わらず有効です。
わずか2行のコードを入れ替えるだけで
遙かに優れたパフォーマンスを得られるようになるのです。

また、若干のハッシュ化やキャッシュを行うことによっても
インクリメンタル集計の利点を得ることができます。
ページが変更された場合でも最小限の計算量で済ませることができるようになります。

なお今回の場合、4コアを駆使しているにも関わらず、
並列mapがそれほど効果的ではない点に注意してください。
確かに ``toArray`` のコストがかかるようになったにもかかわらず
ベストな結果になっていますが、マルチコアマシン上であっても
パフォーマンスはわずかしか改善しないでしょう。
繰り返しますが、最も大きな違いはmap/reduceアプローチに備えられた
分割統治戦略を採用したことにあります。

## 非モノイド準同型

すべてのマッピングが必ずしもモノイド準同型だとは限らないと説明しました。
この節ではそのような実例を見ていくことにします。

今回の例では単語数をカウントするのではなく、
テキストブロック中で最も出現頻度の大きい単語を返すようにします。

基本的なコードは以下の通りです：

```fsharp
module FrequentWordTest =

    open System
    open System.Text.RegularExpressions

    type Text = Text of string

    let addText (Text s1) (Text s2) =
        Text (s1 + s2)

    let mostFrequentWord (Text s) =
        Regex.Matches(s,@"\S+")
        |> Seq.cast<Match>
        |> Seq.map (fun m -> m.ToString())
        |> Seq.groupBy id
        |> Seq.map (fun (k,v) -> k,Seq.length v)
        |> Seq.sortBy (fun (_,v) -> -v)
        |> Seq.head
        |> fst
```

``mostFrequentWord`` 関数は先の ``wordCount`` よりも複雑なので
順を追って説明していきます。

まず正規表現を使って、空白ではないすべての文字にマッチさせます。
この結果は ``Match`` のリストではなく ``MatchCollection`` になるため、
これをシーケンスへと明示的にキャストします
(C#的に言えば ``IEnumerable<Match>`` にします)。

次にそれぞれの ``Match`` に対して ``ToString()`` を使って、
一致した単語へと変換します。
そして単語毎にグループ化した後、 ``(単語,単語のリスト)`` のペアを
要素とするようなリストにします。
その後 ``(単語,リストの要素数)`` となるように変換して、
(単語の数を負数にすることで)逆順にソートします。

最後に1番目のペアを取り出してからペアの1番目の項目を返します。
これが最も出現頻度の大きい単語です。

さて続けましょう。
以前と同じようにページとドキュメントを用意します。
今回はパフォーマンスについては特に問題にしないため、2ページで十分です。
ただし今回の場合には **内容の異なる** 2ページが必要です。
そこで1ページ目は「hello」だけを含み、
2ページ目には「world」だけを含むようにします。
(思うに、あまり面白そうな本ではなさそうですけどね！)

```fsharp
module FrequentWordTest =

    // 前述のコード

    let page1() =
        List.replicate 1000 "hello "
        |> List.reduce (+)
        |> Text

    let page2() =
        List.replicate 500 "world "
        |> List.reduce (+)
        |> Text

    let document() =
        [ page1(); page2() ]
```

「hello」は1000作りましたが、「world」は500しか作っていません。
したがって「hello」が最も出現頻度が高い単語になります。

最後に以前と同じく2つのアプローチを比較してみます。
1番目のアプローチとしてはすべてのページを連結した後に
``mostFrequentWord`` を適用します。
2番目のアプローチではページそれぞれに ``mostFrequentWord`` を適用した後に
結果を連結します。

```fsharp
module FrequentWordTest =

    // 前述のコード

    document()
    |> List.reduce addText
    |> mostFrequentWord
    |> printfn "先に足しあわせた場合の最頻出単語は %s"

    document()
    |> List.map mostFrequentWord
    |> List.reduce (+)
    |> printfn "map reduceした場合の最頻出単語は %s"
```

何が起こるかわかりますか？
1番目のアプローチが正解です。
しかし2番目のアプローチは全く見当外れな答えを返します！

```
先に足しあわせた場合の最頻出単語は hello
map reduceした場合の最頻出単語は helloworld
```

2番目のアプローチでは各ページの最頻出単語を単に連結しただけです。
この文字列はどのページにも **出てこない** 新しいものです。
完全に間違いです！

さて、文字列は連結においてはモノイドであるため、
マッピングはモノイド(Text)からモノイド(文字列)への変換になっています。

しかし今回のマッピングでは「形」が維持されていません。
大きなテキストのチャンク内にある最頻出単語は小さなテキストのチャンク内にある
最頻出単語から派生したものではありません。
つまりモノイド準同型としては適切ではないということです。

したがって、多数のテキストファイルに対して ``mostFrequentWord`` を使って
最頻出単語を見つけ出す場合、残念ながらまずテキストを
すべて無理矢理連結しなければならず、分割統治の戦略による利点も
得られないということになります。

...あるいは何か方法があるかも？
``mostFrequentWord`` を正しいモノイド準同型へと変換する方法がある？
それについてはまた後ほど！

## 次のステップ

これまではきちんとしたモノイドだけを扱ってきました。
しかしモノイド **ではない** ものを扱いたい場合にはどうしたらよいでしょうか？
他には？

シリーズの次回の記事ではほとんどすべてのものを
モノイドへと変換する方法について説明します。

また今回の例にあった ``mostFrequentWord`` を正しいモノイド準同型へと修正して、
ゼロに関するやっかいな問題の再検討と、ゼロを作成するための
エレガントなアプローチについても紹介します。

次回をお楽しみに！

## 参考文献

データ集計におけるモノイドの使用方法について興味があれば、
以下のリンクにある議論が参考になるでしょう：

* Twitter社による [Algebirdライブラリ][link05]
* たいていの [確率的アルゴリズムのためのデータ構造][link06] がモノイドであることについて
* [ガウシアン分布はモノイドを形成する][link07]
* Google社による [MapReduceプログラミングモデル][link08] (PDF)
* [Monoidify! 効率的なMapReduceアルゴリズムのためのデザイン原則としてのモノイド][link09] (PDF)
* LinkedIn社による [Hadoop用Hourglassライブラリ][link10]
* StackExchangeより： [データベースの計算において群やモノイド、環の用途は？][link11]

もっと技術的なことを知りたい場合には
図形ダイアグラムを使ってモノイドや半群について考察した以下の文献が
参考になるでしょう：

* [モノイド：テーマとバリエーション][link12] (PDF)


[link01]: http://fsharpforfunandprofit.com/posts/monoids-part2/ "Monoids in practice"
[link02]: Monoids%20without%20tears.md "難しくないモノイド"
[link03]: http://fsharpforfunandprofit.com/posts/printf/ "Formatted text using printf"
[link04]: https://leanpub.com/understandingfunctionalprogramming?utm_campaign=understandingfunctionalprogramming "Understanding Functional Programming"
[link05]: https://blog.twitter.com/2012/scalding-080-and-algebird "Scalding 0.8.0 and Algebird"
[link06]: http://highlyscalable.wordpress.com/2012/05/01/probabilistic-structures-web-analytics-data-mining/ "Probabilistic Data Structures for Web Analytics and Data Mining"
[link07]: http://izbicki.me/blog/gausian-distributions-are-monoids "Gausian distributions form a monoid"
[link08]: http://citeseerx.ist.psu.edu/viewdoc/download?doi=10.1.1.104.5859&rep=rep1&type=pdf "Google’s MapReduce Programming Model—Revisited"
[link09]: http://arxiv.org/abs/1304.7544 "Monoidify! Monoids as a Design Principle for Efficient MapReduce Algorithms"
[link10]: http://www.slideshare.net/matthewterencehayes/hourglass-27038297 "Hourglass: a Library for Incremental Processing on Hadoop"
[link11]: http://cs.stackexchange.com/questions/9648/what-use-are-groups-monoids-and-rings-in-database-computations
[link12]: http://www.cis.upenn.edu/~byorgey/pub/monoid-pearl.pdf

[img01]: img/02-01.png
[img02]: img/02-02.png
