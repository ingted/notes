# 非モノイドへの対応(翻訳)
難しくないモノイド パート3

原文： [Working with non-monoids][link01]

----

シリーズの [前回の記事][link02] ( [原文][link03] )ではきちんとしたモノイドだけを扱いました。
しかし対象となるものがモノイド **ではない** 場合はどうでしょう？
どうすればいいですか？

そこで今回はほぼありとあらゆるものをモノイドへと変換するようなテクニックを紹介します。

その際に、シングルトンよりもリストを使用したほうがいいだとか、
できるだけオプション型を使用したほうがいいだとかいうような、
一般的かつ重要な関数型のデザインイディオムについても多数紹介します。

## クロージャにする

きちんとしたモノイドについて再確認すると、
モノイドとはクロージャ、結合性、単位元という
3つの要素を持つものでした。
それぞれの条件毎に課題があるため、順を追って説明していきます。

まずはクロージャです。

場合によっては、値同士を足しあわせたいけれども、
連結後の値が元の値とは異なる型になることがあります。
この場合はどのように処理したらよいでしょうか？

1つの方法としては、クロージャになるような新しい型と
元の型とのマッピングを用意します。
このアプローチは前回 ``Customer`` と ``CustomerStats`` の例で紹介した通りです。
元の型に手を加える必要がないため、多くの場合はこの方法が最も簡単です。

一方、どうしても ``map`` を使用したくなく、
その代わりにクロージャの条件に見合うような新しい型を
作りたい場合もあるでしょう。

いずれにしても新しい型を作るか、既存の型を手直しするかしなければいけないので、
クロージャにするためにはそれぞれ同じような方法が使えます。

### 新しい複合型を作成するためにクロージャ型を組み合わせる

ご存じの通り、数値型は足し算やかけ算のような基本的な演算に対しては
クロージャの条件を満たしています。
また、非数値型であっても、文字列やリストであれば連結に対して
クロージャになっています。

このことから分かるのは、これらの型を組み合わせた型も
やはりクロージャであるということです。
これらの型の「add」を使えば、まさに複合型の「add」を定義できるというわけです。

たとえば以下の通りです：

```fsharp
type MyType = { count:int; items:int list }

let addMyType t1 t2 =
    { count = t1.count + t2.count
      items = t1.items @ it2.items }
```

``addMyType`` 関数は ``int`` フィールドに対しては整数の足し算、
``list`` フィールドについてはリストの連結を行っています。
そうすると ``MyType`` は ``addMyType`` に対してクロージャになります。
実際、クロージャというだけでなく、モノイドでもあります。
つまり今回の場合はこれで完了です！

これはまさに [前回の記事][link04] ( [原文][link05] )で ``CustomStats`` に対して
採用したアプローチそのものです。

つまり1つめのヒントです：

* デザインのヒント：モノイド的な型を簡単に作成したければ、
  型の各フィールドもモノイドにすること

そこで問題です：
このようにした場合、新しい複合型の「ゼロ」は何になるのでしょう？

### 非数値型に対処する

上のアプローチは複合型を作る場合には有効です。
しかし明らかに数値的な同値性を持たないような非数値型についてはどうでしょうか？

単純な場合を考えてみましょう。
以下のようにchar同士を足しあわせたいとします：

```fsharp
'a' + 'b' -> 何？
```

しかしchar足すcharは別の文字にはなりません。
それぞれが空でなければstringになります。

```fsharp
'a' + 'b' -> "ab" // クロージャにならない！
```

しかしやっかいなことに、この挙動はクロージャの条件を満たしません。
この問題を修正する1つの方法としては、charをstringに強制的に変換して
クロージャになるようにしてしまうことです：

```fsharp
"a" + "b" -> "ab"
```

しかしこの方法はcharにしか通用しません。
その他の型にも通用するような汎用的な解決策はないのでしょうか？

そこで、少し時間を取って ``string`` と ``char`` との
関係について考えてみてください。
stringはcharのリストあるいは配列だと考えられます。

別の言い方をすれば、以下のようにcharのリストにすればよいというわけです：

```fsharp
['a'] @ ['b'] -> ['a'; 'b'] // リストが正解！
```

こうすればクロージャの条件も満たします。

さらに、実を言うとこの方法は **ほとんどの** 問題に対する解決策になっています。
というのも、リストは **あらゆるもの** を含むことができ、
また(連結に対しては)常にモノイドだからです。

そこで2つめのヒントです：

* デザインのヒント：非数値型をクロージャにする場合、
  単一の項目をリストで置き換えること

場合によってはモノイドにするためにリストを用意した後、
処理が終わった時点で別の型へと変換し直す必要があるかもしれません。

たとえば ``char`` の場合、すべての処理はcharのリストに対して行い、
最後にだけstringへと変換すればよいでしょう。

というわけで、「モノイドchar」モジュールを作ってみましょう。

```fsharp
module MonoidChar =
    open System

    /// "モノイド的char"
    type MChar = MChar of Char list

    /// 1つのcharを"モノイド的char"へと変換する
    let toMChar ch = MChar [ch]

    /// 2つのモノイド的charを足す
    let addChar (MChar l1) (MChar l2) =
        MChar (l1 @ l2)

    /// 中置バージョン
    let (++) = addChar

    /// 文字列へと変換する
    let toString (MChar cs) =
        new String(List.toArray cs)
```

この通り、 ``MChar`` は1つのcharではなく、
charのリストに対するラッパーになっています。

ではテストしてみます：

```fsharp
open MonoidalChar

// 2つのcharを足しあわせてstringに変換する
let a = 'a' |> toMChar
let b = 'b' |> toMChar
let c = a ++ b
c |> toString |> printfn "a + b = %s"
// 結果："a + b = ab"
```

もう少し手を加えるとすれば、以下のようにして
一連のcharに対してmap/reduceを使ってみてもよいでしょう：

```fsharp
[' '..'z']
|> List.filter System.Char.IsPunctuation
|> List.map toMChar
|> List.reduce addChar
|> toString
|> printfn "区切り文字は%s"
// 結果：区切り文字は!"#%&'()*,-./:;?@[\]_
```

### エラーに対するモノイド

``MonoidalChar`` の例ではとても単純で、
おそらくは別の方法を使っても実装することができるでしょう。
しかしこのテクニックは一般に通用するような非常に便利なものです。

たとえばいくつかのバリデーションを行うようなシンプルなモジュールがあるとします。
``Success`` と ``Failure`` という2つのオプションがあり、
``Failure`` にはエラー文字列が関連づけられています。

```fsharp
module Validation =

    type ValidationResult =
        | Success
        | Failure of string

    let validateBadWord badWord (name:string) =
        if name.Contains(badWord) then
            Failure ("文字列に不正な単語が含まれています：" + badWord)
        else
            Success

    let validateLength maxLength name =
        if String.length name > maxLength then
            Failure "文字列が長すぎます"
        else
            Success
```

実際には1つの文字列に対して複数のバリデーションを行い、
最終的には何らかの方法で足しあわされたすべての結果を取得することになるでしょう。

これはまさにモノイドの出番です！
2つの結果の対を足しあわせることができれば、
後はもう任意個の結果を足しあわせられるわけです！

しかし問題があります。
どうやって **2つ** のバリデーション結果を連結すればよいでしょうか？

```fsharp
let result1 = Failure "文字列がnullまたは空です"
let result2 = Failure "文字列が長すぎます"

result1 + result2 = ????
```

愚直なアプローチとしては文字列同士を連結する方法ですが、
ところがこれだとフォーマット文字列を使っていたり、
ローカライゼーション用にリソースIDを使ったりした場合にうまくいきません。

そうではなくて、``Failure`` ケースを単一の文字列ではなく
文字列の **リスト** へと変換すればよいです。
こうすれば連結後の結果も単純になります。

実際のコードは以下のようになります。
リストを使うように ``Failure`` を変更してあります：

```fsharp
module MonoidalValidation =

    type ValidationResult =
        | Success
        | Failure of string list

    // 1つの文字列をFailureケースに変換するためのヘルパ
    let fail str = 
        Failure [str]

    let validateBadWord badWord (name:string) =
        if name.Contains(badWord) then
            fail ("文字列に不正な単語が含まれています：" + badWord)
        else
            Success

    let validateLength maxLength name =
        if String.length name > maxLength then
            fail "文字列が長すぎます"
        else
            Success
```

このコードではそれぞれのバリデーション中で
文字列を指定した ``fail`` を呼び出していますが、
実際にやっていることは文字列のリストとしているだけです。
このようにしておくことによって、バリデーションの結果を
それぞれ連結できるようになるというわけです。

ここまで来れば ``add`` 関数が作成できます。

ロジックは以下の通りです：

* 2つとも ``Success`` の場合、連結後の結果は ``Success``
* 1つが ``Failure`` の場合、連結後の結果は ``Failure``
* 2つとも ``Failure`` の場合、連結後の結果は2つを連結したリストの ``Failure``

コードは以下の通りです：

```fsharp
module MonoidicValidation =

    // 上の通り

    /// 2つの結果を足しあわせる
    let add r1 r2 =
        match r1,r2 with
        | Success,    Success -> Success
        | Failure f1, Success -> Failure f1
        | Success,    Failure f2 -> Failure f2
        | Failure f1, Failure f2 -> Failure (f1 @ f2)
```

ロジックをチェックするテストもいくつか用意しましょう：

```fsharp
open MonoidalValidation

let test1 =
    let result1 = Success
    let result2 = Success
    add result1 result2
    |> printfn "結果は %A"
    // "結果は Success"

let test2 =
    let result1 = Success
    let result2 = fail "文字列が長すぎます"
    add result1 result2
    |> printfn "結果は %A"
    // 結果は Failure ["文字列が長すぎます"]

let test3 =
    let result1 = fail "文字列がnullまたは空です"
    let result2 = fail "文字列が長すぎます"
    add result1 result2
    |> printfn "結果は %A"

    // 結果は Failure
    //   [ "文字列がnullまたは空です";
    //     "文字列が長すぎます" ]
```

もう少し現実的な例として、適用したいバリデーション関数を
リストとして用意してみます：

```fsharp
let test4 =
    let validationResults str =
        [
        validateLength 10
        validateBadWord "monad"
        validateBadWord "cobol"
        ]
        |> List.map (fun validate -> validate str)

    "cobol では monad がネイティブでサポートされています"
    |> validationResults
    |> List.reduce add
    |> printfn "結果は %A"
```

実行結果は3つのエラーメッセージを含んだ ``Failure`` になります：

```
結果は Failure
  ["文字列が長すぎます"; "文字列に不正な単語が含まれています：monad";
   "文字列に不正な単語が含まれています：cobol"]
```

このモノイドの説明を終わらせる前にもう1つ確認しておくべき事があります。
「ゼロ」が必要です。
どんな値になるでしょうか？

定義によると、別の結果と連結すると別の結果をそのまま返すような値になります。

この定義から、「ゼロ」が ``Success`` であると見抜いていただけたでしょうか。

```fsharp
module MonoidalValidation =

    // 上の通り

    // 単位元
    let zero = Success
```

おわかりの通り、reduceの対象が空のリストになっている場合にこのゼロが必要になります。
そこで以下のように、空の ``ValidationResult`` のリストを指定することで、
適用するバリデーション関数が全く無い場合をテストしてみることにします。

```fsharp
let test5 =
    let validationResults str =
        []
        |> List.map (fun validate -> validate str)

    "cobol では monad がネイティブでサポートされています"
    |> validationResults
    |> List.fold add zero
    |> printfn "結果は %A"

    // 結果は Success
```

なおこのコードではランタイムエラーが発生しないよう、
``reduce`` を ``fold`` に書き換えている点に注意してください。

### Listのパフォーマンス

リストを使用することの利点はもう1つあります。
他の連結方法と比較すると、
リストの連結は参照先のオブジェクトを変更したり再配置したりする必要がないため、
計算とメモリ使用率の双方において比較的コストが低く済みます。

たとえば前回の記事では文字列をラップした ``Text`` ブロックを定義して、
これらのコンテンツを文字列として連結していました。

```fsharp
type Text = Text of string

let addText (Text s1) (Text s2) =
    Text (s1 + s2)
```

しかし文字列が大きくなると、このような連続的な結合のコストが高くなります。

たとえば ``Text`` ブロックが代わりに文字列の **リスト** であるような
別の実装を検討してみましょう。

```fsharp
type Text = Text of string list

let addText (Text s1) (Text s2) =
    Text (s1 @ s2)
```

実装としてはほとんど変わりませんが、パフォーマンスは劇的に改善されています。

すべての操作を文字列の **リスト** として処理しておき、
処理工程の最後の最後に通常の文字列へと戻せばよいでしょう。

また、リストでは思うようなパフォーマンスが出せないと思うのであれば、
このアプローチを拡張してツリーやヒープのような古典的データ構造を使うようにしたり、
ResizeArrayのような可変型を使うようにしたりすることも簡単に可能です。
(パフォーマンスについては今回の記事の末尾にある付録でもう少し詳細な説明をしています。)

### 専門用語注意

オブジェクトのリストをモノイドとして使用するというコンセプトは
数学的に一般的なもので、 [「フリーモノイド」][link06] (free monoid)と呼ばれています。
また、コンピュータサイエンスの分野では ``A*`` と書いて
 [「クリーネスター」][link07] (Kleene star)とも呼ばれています。
また、空のリストを許容しない場合には要素がないという状態がありません。
このような亜種は「自由半群」 (free semigroup)あるいは ``A+`` と書いて
「クリーネプラス」 (Kleene plus)と呼ばれます。

「スター」や「プラス」という記法については正規表現の経験があれば
お馴染みのものでしょう。 [*](#note01)

<a name="note01">*</a>
正規表現とモノイドとの間に関連があるなどとはきっと思いもよらなかったことでしょう！
実のところ、両者には [さらに深い関係][link08] があります。

## 結合性

クロージャの説明が終わったので、次は結合性です。

連載記事のかなり最初の方に引き算や割り算など、結合性の無い演算子が出てきました。

つまり ``5 - (3 - 2)`` と ``(5 - 3) - 2`` とは同じ値ではありません。
したがって引き算には結合性がないことがわかります。
また、 ``12 / (3 / 2)`` と ``(12 / 3) / 2`` も異なるため、割り算にも結合性がありません。

これらに対する正しい1つの答えはありません。
というのも、左から右にかけて計算するのか、右から左にかけて計算するのかによって
答えが異なるという問題をきちんと検討しなければいけないからです。

事実、F#の標準ライブラリにはどちらでも好きな方を選択できるように
2つのバージョンの ``fold`` と ``reduce`` が用意されています。
一般的な ``fold`` や ``reduce`` は左から右に処理が進みます：

```fsharp
// (12 - 3) - 2 と同じ
[12;3;2] |> List.reduce (-) // => 7

// ((12 - 3) - 2) - 1 と同じ
[12;3;2;1] |> List.reduce (-) // => 6
```

しかし ``foldBack`` や ``reduceBack`` の場合は右から左に処理が進みます：

```fsharp
// 12 - (3 - 2) と同じ
[12;3;2] |> List.reduceBack (-) // => 11

// 12 - (3 - (2 - 1)) と同じ
[12;3;2;1] |> List.reduceBack (-) // => 10
```

したがって、結合性の条件を満たすと言うことはつまり
``fold`` と ``foldBack`` のどちらを使っても
**同じ** 結果が必ず得られるということだと言えます。

### 演算子を要素に組み込む

しかし一貫性のあるモノイド的アプローチ **こそ** が必要なのだとすると、
多くのケースで通用するようなアプローチとしては
それぞれの要素の性質として演算子を組み込んでしまうとよいでしょう。
演算子を動詞ではなく名詞にしてしまうのです。

たとえば ``3 - 2`` は ``3 + (-2)`` ともみなせます。
「引く」という動詞ではなく、「マイナス2」という名詞とするわけです。

今回の場合、上の例は ``5 + (-3) + (-2)`` となります。
すると演算子が足し算になり、足し算には **確かに** 結合性があるので、
``5 + (-3 + -2)`` は ``(5 + -3) + -2`` と同じ結果になります。

同じ事が割り算にも当てはまります。
``12 / 3 / 2`` は ``12 * (1/3) * (1/2)`` と変換でき、
演算子をかけ算とすることができます。
したがって結合性を持つようになりました。

この演算子を要素の性質として組み込んでしまうというアプローチは
うまい具合に一般化できます。

つまり次のようなヒントです：

* デザインのヒント：ある演算子に対する結合性が必要になる場合、
  演算子をオブジェクトへと組み込んでみること

具体的な動作を確認するために、以前の例を再度利用してみます。
皆さんが覚えているかわかりませんが、最初の記事では文字列に対して
結合性を持たない例として ``subtractChars`` を検討しました。

``subtractChars`` を単純に実装すると以下のようになります：

```fsharp
let subtractChars (s1:string) (s2:string) =
    let isIncluded (ch:char) = s2.IndexOf(ch) = -1
    let chars = s1.ToCharArray() |> Array.filter isIncluded
    System.String(chars)

// 中置バージョン
let (--) = subtractChars
```

この実装を使っていくつかテストしてみます：

```fsharp
"abcdef" -- "abd"   // "cef"
"abcdef" -- ""      // "abcdef"
```

また、結合性の条件を満たしていないことも確認できます：

```fsharp
("abc" -- "abc") -- "abc"   // ""
"abc" -- ("abc" -- "abc")   // "abc"
```

どうすれば結合性を持てるようにできるでしょうか？

ヒントは先ほど数値に対して行ったように、「引き算性」を
演算子からオブジェクトへと組み込むことでした。

つまり以下のように、削除したい文字列をキャプチャしているような
「引き算」あるいは「文字削除」データ構造として文字列を置き換えればよいわけです：

```fsharp
let removalAction = (subtract "abd") // データ構造
```

そしてこのデータ構造を文字列に対して「適用」します：

```fsharp
let removalAction = (subtract "abd")
removalAction |> applyTo "abcdef" // 結果は "cef"
```

このアプローチを使用すると、先ほどは結合性を持たなかった例であっても
以下のようにして結合性を持たせることができるようになります：

```fsharp
let removalAction = (subtract "abc") + (subtract "abc") + (subtract "abc")
removalAction |> applyTo "abc"  // 結果は ""
```

確かにこのコードはオリジナルのコードとは厳密に同じではありませんが、
こちらの方が実際には多くの場合に適合することがわかるでしょう。

実装は以下の通りです。
``CharsToRemove`` には文字セットが含まれるように定義してあり、
その他の関数についてもそれに応じた自然なものになっています。

```fsharp
/// 削除する文字のリストを保持する
type CharsToRemove = CharsToRemove of Set<char>

/// 新しいCharsToRemoveのインスタンスを生成する
let subtract (s:string) =
    s.ToCharArray() |> Set.ofArray |> CharsToRemove

/// CharsToRemoveを文字列に適用する
let applyTo (s:string) (CharsToRemove chs) =
    let isIncluded ch = Set.exists ((=) ch) chs |> not
    let chars = s.ToCharArray() |> Array.filter isIncluded
    System.String(chars)

/// 2つのCharsToRemoveを連結して新しいものを返す
let (++) (CharsToRemove c1) (CharsToRemove c2) =
    CharsToRemove (Set.union c1 c2)
```

テストしてみましょう！

```fsharp
let test1 =
    let removalAction = (subtract "abd")
    removalAction |> applyTo "abcdef" |> printfn "結果は%s"
    // 「結果はcef」

let test2 =
    let removalAction = (subtract "abc") ++ (subtract "abc")
    removalAction |> applyTo "abc" |> printfn "結果は%s"
    // 「結果は」
```

このアプローチはある意味で、 **データ** ではなく
**アクション** をモデル化しているものだと考えるとよいでしょう。
``CharsToRemove`` アクションのリストがあり、これらを1つの「巨大な」
``CharsToRemove`` アクションとして連結し、
最終的にはこの1つのアクションを実行することで
途中の計算が完了しています。

もう1つ別の例を紹介しますが、この時点で
「これはどうやら関数っぽいものじゃないだろうか？」と思うかもしれません。
果たしてその答えは「その通り！」なのです。

実際、 ``CharsToRemove`` というデータ構造を作成するよりも、
以下のようにして元々の ``subtractChars`` に対して部分適用することができます：

(なお簡単に部分適用できるようにするためパラメータの順序を入れ替えています)

```fsharp
// 部分適用のためにパラメータを逆順にする
let subtract str charsToSubtract =
    subtractChars charsToSubtract str

let removalAction = subtract "abd"
"abcdef" |> removalAction |> printfn "結果は%s"
// 「結果はcef」
```

また、特別な ``applyTo`` 関数も不要です。

しかし引き算用の関数が複数ある場合にはどうすればいいでしょうか？
それぞれの部分適用関数は ``string -> string`` というシグネチャを持ちますが、
どうやってそれぞれを「足し算」したらよいのでしょう？

```fsharp
(subtract "abc") + (subtract "abc") + (subtract "abc") = ?
```

答えはもちろん関数合成です！

```fsharp
let removalAction2 = (subtract "abc") >> (subtract "abc") >> (subtract "abc")
removalAction2 "abcdef" |> printfn "結果は%s"
// 「結果はdef」
```

これは ``CharsToRemove`` のデータ構造を作成した場合と機能的に同じです。

「アクションとしてのデータ構造」と関数によるアプローチは厳密には同じではありません。
たとえば ``CharsToRemove`` はsetを使用していて、
最終的には文字列にしか適用できないのでこちらの方が効率的だと言えます。
しかしいずれにおいても最終的には同じ結果が得られます。
どちらが適切なのかは対象となる問題によって異なります。

関数とモノイドの関係については次回の記事でもう少し説明する予定です。

## 単位元

では最後の条件である単位元を見ていきます。

これまでにも説明した通り、単位元は必須の条件ではありませんが
空のリストを扱うことがある場合には有った方が便利です。

数値型の値の場合、演算子に対する単位元は比較的簡単に見つけられます。
(足し算の場合は) ``0``、(かけ算の場合は) ``1``、
(maxの場合は) ``Int32.MinValue`` のいずれかであることがほとんどです。

また、数値型しか含まないような構造体の場合にも同じことがいえます。
構造体のすべてのメンバに対して、適切な単位元を設定すればよいのです。
これはまさに前回の記事にあった ``CustomerStats`` 型がよい例です。

しかし数値型ではないものを含むオブジェクトの場合はどうすればよいでしょうか？
候補として適切そうなものがない場合に「ゼロ」あるいは単位元を
どうやって作成すればよいでしょう？

正解は「でっち上げる」です。

大まじめですよ！

具体的な例は前回の記事にあったもので、
``OrderLine`` 型における ``EmptyOrder``が該当します：

```fsharp
type OrderLine =
    | Product of ProductLine
    | Total of TotalLine
    | EmptyOrder
```

もう少し詳しく説明しましょう。
手順としては2つありました：

* まず、新しいケースを作成して、それを(上のように) ``OrderLine`` の代用品として追加
* 次に、(以下のように) ``addLine`` 関数がこのケースを処理するように変更

```fsharp
let addLine orderLine1 orderLine2 =
    match orderLine1, orderLine2 with
    // どちらかがゼロ？その場合は他方を返す
    | EmptyOrder, _ -> orderLine2
    | _, EmptyOrder -> orderLine1
    // その他のロジック...
```

これだけです。

新しく拡張した型は古い注文品目のケースとも整合性がある上、
**さらに** 新しいケースにも対応します。
したがって古いケースに対する挙動の大部分が再利用できます。

特に、新しく拡張した型はすべてのモノイド規則を満たしていることがわかるでしょうか？

* 新しい型の値の対は足しあわせると別の新しい値が得られる(クロージャ)
* 旧い型において連結の順序が不同であれば新しい型でも順不同(結合性)
* そして最後に...追加したケースが新しい型の単位元になる

### PositiveNumberをモノイドに変換する

これまでに紹介した半群に対しても同じ事ができます。

たとえば厳密に正の整数には(足し算に対して)単位元がありません。
したがってこれは半群です。
そこで「特別なケースを追加する」テクニックを使って
(単なる ``0`` ではない！)ゼロを作成したい場合には、
特別な(整数ではない) ``Zero`` ケースを追加して、
このケースを処理するように ``addPositive`` 関数を以下のように修正します：

```fsharp
type PositiveNumberOrIdentity =
    | Positive of int
    | Zero

let addPositive i1 i2 =
    match i1, i2 with
    | Zero, _ -> i2
    | _, Zero -> i1
    | Positive p1, Positive p2 -> Positive (p1 + p2)
```

確かに ``PositiveNumberOrIdenitty`` の例は恣意的ではありますが、
「普通」の値と特別なゼロ値が別個に必要な場合には同じアプローチが
利用できるということがわかると思います。

### 一般的な解決策

この方法にはいくつか欠点があります：

* 通常ケースとゼロケースの **2つ** のケースを処理しなければならない
* 独自の型と独自の足し算用関数を用意しなければならない

残念ながら1番目の問題については対処しようがありません。
自然なゼロの無いシステムにおいて、ゼロを人為的に作成した場合には
常に2つのケースを処理することになります。

しかし2番目の問題についてはある程度の対策があります！
新しい独自の型を毎回作成するのではなく、
2つのケースを持った **ジェネリック** 型を作成すればよいのです。
つまり以下のように、1つは普通の値に対するケース、
もう1つは人為的に作成したゼロに対するケースにします：

```fsharp
type NormalOrIdentity<'T> =
    | Normal of 'T
    | Zero
```

このような型を見たことがありませんか？
これはまさにOption型そのものです！

つまり通常の値セットからは外れるような単位元が必要であれば、
``Option.None`` を使ってそれを表すことができます。
そして、その他すべての通常の値に対しては ``Option.Some`` を使います。

``Option`` 型を使うことのもう1つの利点は、
同じく完全にジェネリックな「足し算」関数を作成できるということです。
まずは以下のようにしてみます：

```fsharp
let optionAdd o1 o2 =
    match o1, o2 with
    | None, _ -> o2
    | _, None -> o1
    | Some s1, Some s2 -> Some (s1 + s2)
```

ロジックは単純です。
オプションのいずれかが ``None`` であれば他方のオプションを返します。
両方が ``Some`` の場合はそれぞれラップされた値を解除してから足しあわせ、
その後再び ``Some`` にラップします。

しかし最後の行にある ``+`` によって足しあわせる型が仮定されてしまいます。
そこで以下のように足し算用の関数を明示的に指定できるようにするとよいでしょう：

```fsharp
let optionAdd f o1 o2 =
    match o1, o2 with
    | None, _ -> o2
    | _, None -> o1
    | Some s1, Some s2 -> Some (f s1 s2)
```

実際には足し算用の関数を部分適用したものを使うことになるでしょう。

さて以上でもう1つ重要なヒントが得られました：

* デザインのヒント：演算子に対する単位元を用意する場合、
  判別共用体に特別なケースを追加する、あるいはもっと単純にはOption型を使用する

### PositiveNumber再考

そこで再び正の整数の例を ``Option`` 型で実装してみましょう。

```fsharp
type PositiveNumberOrIdentity = int option
let addPositive = optionAdd (+)
```

かなりシンプルになりました！

ちなみに ``optionAdd`` のパラメータにはPositiveNumberが計算できるように
「本物の」足し算用関数を渡していることに注意してください。
別の場面においては、半群に関連のある集計用の関数を同じようにして
渡す事になるでしょう。

部分適用の結果、 ``addPositive`` は ``int option -> int option -> int option``
というシグネチャを持つようになり、これはまさに期待した通り
モノイドの加算関数になっていることがわかります。

別の言い方をすると、 ``optionAdd`` は ``'a -> 'a -> 'a`` という任意の関数を
**同じ** 関数へと変換しますが、それぞれにオプション型を「付加」して
``'a option -> 'a option -> 'a option`` というシグネチャにします。

では試してみましょう！
以下のようなテストコードを用意してみます：

```fsharp
// 値をいくつか作成
let p1 = Some 1
let p2 = Some 2
let zero = None

// 足し算のテスト
addPositive p1 p2
addPositive p1 zero
addPositive zero p2
addPositive zero zero
```

このように、 ``None`` を単位元とするためには
残念ながら通常の値を ``Some`` でラップしなければいけません。

面倒くさいように思えるかもしれませんが、実際にはそれほどたいした話ではありません。
以下のコードではそれぞれ異なるリストに対して総和を計算しています。
1つめは空ではないリストの和、2つめは空のリストの和です。

```fsharp
[1..10]
|> List.map Some
|> List.fold addPositive zero

[]
|> List.map Some
|> List.fold addPositive zero
```

### ValidationResult再考

リストをクロージャにするという説明をした際に使用した
``ValidationResult`` 型についても再度検討してみましょう。
この型の定義は以下の通りです：

```fsharp
type ValidationResult =
    | Success
    | Failure of string list
```

では正の整数の場合と同じく、この型を別の視点で検討してみましょう。

この型には2つのケースがあります。
一方には関心の対象となるデータがあり、他方はデータを持ちません。
しかし本当に処理すべきデータはエラーメッセージであって、
成功のケースでは不要です。
かのLeo Tolstoyの言葉を借りれば、
「成功したバリデーションはどれも似たり寄ったりだ。
しかし失敗したバリデーションの理由は実に様々だ。」というわけです。

というわけで、この型を「結果」として考えるのでは無くて、
**失敗を保持する** 型とみなすようにして、
失敗ケースを最初に来るように書き直してみます：

```fsharp
type ValidationFailure =
    | Failure of string list
    | Success
```

そうするとかなりそれらしくなった気がしませんか？

そうです！
ここでもまたオプション型の出番です！
もうこれ無しではいられないような気がしませんか？

オプション型を使うと ``ValidateionFailure`` 型の定義を
以下のように簡略化できます：

```fsharp
type ValidationFailure = string list option
```

文字列を失敗ケースに変換するためのヘルパー関数も
以下のようにリストを ``Some`` にするだけです：

```fsharp
let fail str =
    Some [str]
```

そして「足し算」関数では再び ``optionAdd`` を使いますが、
今回はリストの結合によって内部データを処理します：

```fsharp
let addFailure f1 f2 = optionAdd (@) f1 f2
```

最後に、以前は ``Success`` として定義していたケースは
新しい実装では単に ``None`` になります。

以下は全体のコードにテストを追加したものです：

```fsharp
module MonoidalValidationOption =

    type ValidationFailure = string list option

    // 文字列から失敗ケースに変換するためのヘルパー
    let fail str =
        Some [str]

    let validateBadWord badWord (name:string) =
        if name.Contains(badWord) then
            fail ("文字列に不正な単語が含まれています：" + badWord)
        else
            None

    let validateLength maxLength name =
        if String.length name > maxLength then
            fail "文字列が長すぎます"
        else
            None

    let optionAdd f o1 o2 =
        match o1, o2 with
        | None, _ -> o2
        | _, None -> o1
        | Some s1, Some s2 -> Some (f s1 s2)

    /// optionAddを使って2つの結果を足す
    let addFailure f1 f2 = optionAdd (@) f1 f2

    // ゼロを定義
    let Success = None

module MonoidalValidationOptionTest =
    open MonoidalValidationOption

    let test1 =
        let result1 = Success
        let result2 = Success
        addFailure result1 result2
        |> printfn "結果は %A"
        // 結果は <null>

    let test2 =
        let result1 = Success
        let result2 = fail "文字列が長すぎます"
        addFailure result1 result2
        |> printfn "結果は %A"
        // 結果は Some ["文字列が長すぎます"]

    let test3 =
        let result1 = fail "文字列がnullまたは空です"
        let result2 = fail "文字列が長すぎます"
        addFailure result1 result2
        |> printfn "結果は %A"
        // 結果は Some ["文字列がnullまたは空です"; "文字列が長すぎます"]

    let test4 =
        let validationResults str =
            [
            validateLength 10
            validateBadWord "monad"
            validateBadWord "cobol"
            ]
            |> List.map (fun validate -> validate str)

        "cobol では monad がネイティブでサポートされています"
        |> validationResults
        |> List.reduce addFailure
        |> printfn "結果は %A"
        // 結果は Some
        //   ["文字列が長すぎます"; "文字列に不正な単語が含まれています：monad";
        //    "文字列に不正な単語が含まれています：cobol"]

    let test5 =
        let validationResults str =
            []
            |> List.map (fun validate -> validate str)

        "cobol では monad がネイティブでサポートされています"
        |> validationResults
        |> List.fold addFailure Success
        |> printfn "結果は %A"
        // 結果は <null>
```

## デザインのヒントのまとめ

ここで小休止して、これまでをまとめておきましょう。

デザインのヒントをまとめると以下の通りです：

* デザインのヒント：モノイド的な型を簡単に作成したければ、
  型の各フィールドもモノイドにすること
* デザインのヒント：非数値型をクロージャにする場合、
  単一の項目をリストで置き換えること
* デザインのヒント：ある演算子に対する結合性が必要になる場合、
  演算子をオブジェクトへと組み込んでみること
* デザインのヒント：演算子に対する単位元を用意する場合、
  判別共用体に特別なケースを追加する、あるいはもっと単純にはOption型を使用する

続く2つの節ではこれまでの記事で紹介した
「平均」と「最頻出単語」という非モノイドの例に対して
これらのヒントを適用してみます。

## ケーススタディ：平均

さてこれでやっかいな平均の問題をなんとかするためのツールが用意できました。

以下のコードでは1対の平均を単純に計算する関数を実装しています：

```fsharp
let avg i1 i2 =
    float (i1 + i2) / 2.0

// テスト
avg 4 5 |> printnf "平均は %g"
// 平均は 4.5
```

最初の記事でも説明した通り、 ``avg`` はモノイドの条件を3つとも満たしません！

まずクロージャではありません。
2つの整数を ``avg`` で組み合わせた後の結果は整数ではありません。

次にもしこれがクロージャだったとしても、 ``avg`` には結合性がありません。
浮動小数点数用の同じような関数 ``avgf`` からも確認できます：

```fsharp
let avgf i1 i2 =
    (i1 + i2) / 2.0

// テスト
avgf (avgf 1.0 3.0) 5.0  |> printfn "左優先の平均は %g"
avgf 1.0 (avgf (3.0 5.0) |> printfn "右優先の平均は %g"

// 左優先の平均は 3.5
// 右優先の平均は 2.5
```

最後に、単位元もありません。

任意の数との平均を計算しても、常に元の数を返すような値があるのでしょうか？
答えは「ありません！」

### デザインのヒントを適用する

ではデザインのヒントが解決策になるかどうかみてみることにしましょう。

* モノイド的な型を簡単に作成したければ、
  型の各フィールドもモノイドにすること

「平均」は数学的な演算なので、モノイド的な同値性はおそらく
数値を元にしたものになるだろうと期待できます。

* 非数値型をクロージャにする場合、
  単一の項目をリストで置き換えること

これはぱっと見では関係がなさそうなので、今のところは無視します。

* ある演算子に対する結合性が必要になる場合、
  演算子をオブジェクトへと組み込んでみること

これは難しい！
どうすれば「平均をとる」という動詞(演算子)を
名詞(データ構造)に置き換えられるのでしょう？

答えは、実際に平均を持つのではなく、「後から平均を求める」ような
構造体を用意すればよいのです。
平均の値が必要になった時点ですべての計算をするようにするわけです。

つまり合計と個数という **2つの** コンポーネントを持ったデータ構造を用意します。
これらの値を使えば必要になった時点で平均を計算できます。

```fsharp
// 平均の計算に必要な情報を保持する
type Avg = { total:int; count:int }

// 2つのAvgを足しあわせる
let addAvg avg1 avg2 =
    { total = avg1.total + avg2.total;
      count = avg1.count + avg2.count }
```

このようにすると ``float`` ではなく ``int`` を保持することができます。
つまり浮動小数点数の足し算による精度の消失を考慮する必要がなくなります。

最後のヒントです：

* 演算子に対する単位元を用意する場合、
  判別共用体に特別なケースを追加する、あるいはもっと単純にはOption型を使用する

今回の場合、このヒントは不要です。
2つのコンポーネントを0にすれば簡単にゼロが用意できます。

```fsharp
let zero = { total=0; count=0 }
```

ゼロとして ``None`` を使ってもいいのですが、
今回の場合はやりすぎでしょう。
リストが空の場合、実際には計算できないとしても
正しい結果を表す ``Avg`` にできます。

データ構造の詳細が説明できたので、残りの実装は簡単でしょう。
以下は実装コードにテストを追加したものです：

```fsharp
module Average =

    // 平均の計算に必要な情報を保持する
    type Avg = { total:int; count:int }

    // 2つのAvgを足しあわせる
    let addAvg avg1 avg2 =
        { total = avg1.total + avg2.total;
          count = avg1.count + avg2.count }

    // 足し算の中置バージョン
    let (++) = addAvg

    // 1つの数字から平均にする
    let avg n = { total=n; count=1 }

    // データから平均を計算する。
    // 空のリストに足しては0を返す
    let calcAvg avg =
        if avg.count = 0
        then 0.0
        else float avg.total / float avg.count

    // 別の方法 - 空のリストに対してはNoneを返す
    let calcAvg2 avg =
        if avg.count = 0
        then None
        else Some (float avg.total / float avg.count)

    // 単位元
    let zero = { total=0; count=0 }

    // テスト
    addAvg (avg 4) (avg 5)
    |> calcAvg
    |> printfn "平均は %g"
    // 平均は 4.5

    (avg 4) ++ (avg 5) ++ (avg 6)
    |> calcAvg
    |> printfn "平均は %g"
    // 平均は 5

    [1..10]
    |> List.map avg
    |> List.reduce addAvg
    |> calcAvg
    |> printfn "平均は %g"
    // 平均は 5.5
```

上のコードにある ``calcAvg`` では ``Avg`` を使って
(浮動小数点数となる)平均を計算しています。
このアプローチの利点は、ゼロ除算の処理を後回しにできるというところです。
単に ``0`` を返すようにしたり、 ``None`` を返すようにしたり、
あるいは計算を無限に延期しておいて、
平均を計算できるようになった時にようやく計算したりすることすらできます！

そしてもちろん、この実装はモノイドになっているわけなので、
「平均」をインクリメンタルに計算することもできます。

つまり100万の数字の平均が既に計算済みだとして、
そこにもう1つ数字を足した場合であっても、
すべてを再計算する必要はありません。
これまで足しあわせたものに新しい1つを追加するだけでいいのです。

## メトリクスに対する若干の注意

もしも皆さんが既に何らかのサーバーやサービスを管理しているのであれば、
CPUやI/Oなどに対するメトリクスをログに記録したり、モニターしたりすることの
重要性についてはご存じのことだろうと思います。

その際によく耳にする質問としては、メトリクスをデザインする
うまい方法がないだろうかというものでしょう。

秒単位のキロバイトがいいのか、
それともサーバーが起動してからの合計キロバイトがいいのでしょうか？
1時間単位の訪問者数と合計訪問者数のどちら？

もしも既に [メトリクスの作成に関するガイドライン][link09] 等を
読んだことがあるのであれば、一般的には **変化率** ではなく
**カウント数** をメトリクスの対象とすべきことがおわかりでしょう。

カウント数の平均値においては、
 (a) データの欠損があったとしてもたいした影響がなく、
 (b) 分単位、時間単位、あるいは何か別の比率に応じて、後から様々な方法で集計できます。

今回のシリーズの説明を受けると、推薦されているこの方法からも
まさにメトリクスがモノイドであるべきだということがわかるでしょう。

先ほどのコードでは「平均」を「合計(total)」と「カウント(count)」の
2つのコンポーネントに変換しましたが、
これはまさにメトリクスを作り出すにはうってつけの方法だったわけです。

平均やその他の比率はモノイドではありません。
しかし「合計」や「カウント」はモノイドです。
そして「平均」は任意のタイミングでこれらの値から計算することができます。

## ケーススタディ：「最頻出単語」をモノイド準同型にする

前回の記事では「最頻出単語」を見つける関数を実装しましたが、
それがモノイド準同型でないことも確認しました。
つまり：

```fsharp
mostFrequentWord(text1) + mostFrequentWord(text2)
```

この結果は以下の式とは同じに **なりません** ：

```fsharp
mostFrequentWord( text1 + text2 )
```

今回もやはり、デザインのヒントを使えばこれらがうまく機能するようになります。

「平均」の例と同じく、ここでも計算を最後まで遅延させるというところが重要です。

最頻出単語を事前に計算するのではなく、
最頻出単語を後で計算するために必要なすべての情報を持ったデータ構造を用意します：

```fsharp
module FrequentWordMonoid =

    open System
    open System.Text.RegularExpressions

    type Text = Text of string

    let addText (Text s1) (Text s2) =
        Text (s1 + s2)

    // 単語の頻出度マップを返す
    let wordFreq (Text s) =
        Regex.Matches(s, @"\S+")
        |> Seq.cast<Match>
        |> Seq.map (fun m -> m.ToString())
        |> Seq.groupBy id
        |> Seq.map (fun (k,v) -> k,Seq.length v)
        |> Map.ofSeq
```

上のコードでは、単語を1つ返すのではなく ``Map<string,int>`` を返す関数
``wordFreq`` を新しく定義しています。
したがって、それぞれのスロットには単語とそれに対応した出現頻度を持つような
ディクショナリを処理することになります。

具体的には以下のようにします：

```fsharp
module FrequentWordMonoid =

    // 上と同じコード

    let page1() =
        List.replicate 1000 "hello world "
        |> List.reduce (+)
        |> Text

    let page2() =
        List.replicate 1000 "goodbye world "
        |> List.reduce (+)
        |> Text

    let page3() =
        List.replicate 1000 "foobar "
        |> List.reduce (+)
        |> Text

    let document() =
        [ page1(); page2(); page3() ]

    // 単語頻出度マップをいくつか表示
    page1() |> wordFreq |> printfn "page1の頻出度マップは %A"
    page2() |> wordFreq |> printfn "page2の頻出度マップは %A"

    // page1の頻出度マップは map [("hello", 1000); ("world", 1000)]
    // page2の頻出度マップは map [("goodbye", 1000); ("world", 1000)]

    document()
    |> List.reduce addText
    |> wordFreq
    |> printfn "documentの頻出度マップは %A"


    // documentの頻出度マップは map [
    //     ("foobar", 1000); ("goodbye", 1000);
    //     ("hello", 1000); ("world", 2000)]
```

このマップ構造を使うと、2つのマップを足しあわせる ``addMap`` を作れるようになります。
この関数は2つのマップにある単語の出現頻度を単にマージするだけです。

```fsharp
module FrequentWordMonoid =

    // 上と同じコード

    // マップ用の足し算を定義
    let addMap map1 map2 =
        let increment mapSoFar word count =
            match mapSoFar |> Map.tryFind word with
            | Some count' -> mapSoFar |> Map.add word (count + count')
            | None -> mapSoFar |> Map.add word count

        map2 |> Map.fold increment map1
```

すべてのマップを連結すると、
マップ全体を走査して出現頻度が最大の単語を見つけ出すことで
最頻出単語が計算できます。

```fsharp
module FrequentWordMonoid =

    // 上と同じコード

    // 最後の処理として
    // マップの中から最頻出単語を見つける
    let mostFrequentWord map =
        let max (candidateWord, maxCountSoFar) word count =
            if count > maxCountSoFar
            then (word, count)
            else (candidateWord, maxCountSoFar)

        map |> Map.fold max ("無し", 0)
```

この新しいアプローチを使って、2つのシナリオを再度検討してみましょう。

1番目のシナリオではすべてのページを1つのテキストとしてまとめた後、
``wordFreq`` を使って出現頻度マップを取得し、
それから ``mostFrequentWord`` を使って最頻出単語を取得します。

2番目のシナリオではそれぞれのページに対して個別に ``wordFreq`` を適用します。
これらのマップを ``addMap`` で連結して1つのグローバルなマップにします。
そして最後の手順として、1番目と同じく ``mostFrequentWord`` を適用します。

```fsharp
module FrequentWordMonoid =

    // 上と同じコード

    document()
    |> List.reduce addText
    |> wordFreq
    // 巨大なマップから最頻出単語を取得
    |> mostFrequentWord
    |> printfn "先に足しあわせた場合の最頻出単語は %A"

    // 先に足しあわせた場合の最頻出単語は ("world", 2000)

    document()
    |> List.map wordFreq
    |> List.reduce addMap
    // マージ後の小さなマップから最頻出単語を取得
    |> mostFrequentWord
    |> printfn "map reduceした場合の最頻出単語は %A"

    // map reduceした場合の最頻出単語は ("world", 2000)
```

このコードを実行すると、今回はどちらも **同じ** 結果になることがわかるでしょう。

つまり ``wordFreq`` は確かにモノイド準同型で、
並列実行したりインクリメンタルに処理できるようになったというわけです。

## 次回予告

今回の記事にはかなり大量のコードがありましたが、
いずれもデータ構造に注目するものばかりでした。

しかしモノイドの定義からすると、連結できるものがデータ構造だけに
限定されるとは言われていません。
**ありとあらゆるもの** が組み合わせられるのです。

次回は型や関数など、その他のオブジェクトに対してモノイドを適用する方法を紹介します。

## 付録：パフォーマンスについて

上の例では2つのリストを「足す」際に、数値を ``+`` で足すのと同じようにして
頻繁に ``@`` で足しています。
これは数値の足し算や文字列の連結といった別のモノイド的な演算子と
簡単に比較できるようにすることが目的でした。

上の例はあくまでも教育用の例に過ぎず、
実世界で磨き上げられた立派なモデルには必ずしもなっておらず、
コード自体も製品向けとしてはあまりに大雑把すぎるものであることを
理解いただければと思います。

何人かの方から、リストの追加( ``@`` )は一般的に避けるべきだという指摘を受けました。
これは1番目のリスト全体をコピーする必要があるためで、
あまり効率のよい操作ではありません。

リストに何かを追加する場合に最も適した方法は
いわゆる「cons」(コンス)の機能を使うことです。
F#の場合は ``::`` です。
F#のリストはリンクリストとして実装されているため、
非常に低いコストで先頭へ要素を追加できます。

このアプローチを採用する場合の欠点としては、対称性が損なわれるという点です。
この演算子は2つのリストを1つに足しあわせるのではなく、
リストと1つの要素との組み合わせでしか機能しません。
つまりモノイドにおける「足し算」としては機能しないのです。

分割統治などのような、モノイドの利点が不要であれば
このcons演算子を採用した方がデザイン的にも妥当です。
必要のない利点を得るためにパフォーマンスを犠牲にする必要は全くありません。

``@`` を使用しない別の方法としては、
そもそもリストを使わないことです！

### リストの代用

``ValidationResult`` のデザイン時には、結果を簡単に集計できるように
エラーになった結果を保持するようにしました。
しかし ``list`` 型を採用したのは単にこれがF#における
デフォルトのコレクション型だったからです。
シーケンスや配列、集合を使った場合でも同じ事が実現できます。
コレクション型であればこれ以外のものであっても問題ないでしょう。

しかしすべての型で同じパフォーマンスが得られるわけではありません。
たとえば2つのシーケンスを連結する処理は遅延的に処理されます。
すべてのデータをコピーする必要はありません。
1つのシーケンスを走査した後、もう一方のシーケンスを走査するだけです。
それならばきっとより高速なのではないでしょうか？

推測だけでは何もならないので、様々なサイズで様々なコレクション型のリストを用意して
パフォーマンスを測定するための小さなテストスクリプトを作成してみました。

モデルとしては非常に単純なものを採用しています。
要素を **1つ** だけ持つようなコレクションをそれぞれの要素とするような
オブジェクトのリストを用意します。
そしてこのコレクションのリストを元に、適切なモノイド演算を行って
1つの巨大なコレクションにしています。
最後に巨大なコレクション全体を1度走査しています。

このデザインは ``ValidationResult`` に大変よく似ています。
``ValidationResult`` ではすべての結果を1つの結果リストとしてまとめた後、
(おそらくは)リスト全体を走査してエラーを表示することになります。

また、先ほどの「最頻出単語」の例にも似ています。
このときにはそれぞれの出現頻度のマップを1つのマップとして連結し、
連結後のマップを走査して最頻出単語を見つけていました。
当然その際には ``list`` ではなく ``map`` を使いましたが、
一連の手順としては同じものです。

### パフォーマンスの測定

さてコードは以下の通りです：

```fsharp
module Performance =

    let printHeader() =
        printfn "Label,ListSize,ReduceAndIterMs"

    // 特定のサイズのリストに対するreduceとiterにかかる時間を計測して出力する
    let time label reduce iter listSize =
        System.GC.Collect() // 開始前にクリーンアップする
        let stopwatch = System.Diagnostics.Stopwatch()
        stopwatch.Start()
        reduce() |> iter
        stopwatch.Stop()
        printfn "%s,%iK,%i" label (listSize/1000) stopwatch.ElapsedMilliseconds

    let testListPerformance listSize =
        let lists = List.init listSize (fun i -> [i.ToString()])
        let reduce() = lists |> List.reduce (@)
        let iter = List.iter ignore
        time "List.@" reduce iter listSize

    let testSeqPerformance_Append listSize =
        let seqs = List.init listSize (fun i -> seq { yield i.ToString() })
        let reduce() = seqs |> List.reduce Seq.append
        let iter = Seq.iter ignore
        time "Seq.append" reduce iter listSize

    let testSeqPerformance_Yield listSize =
        let seqs = List.init listSize (fun i -> seq { yield i.ToString() })
        let reduce() = seqs |> List.reduce (fun x y -> seq { yield! x; yield! y })
        let iter = Seq.iter ignore
        time "Seq(yield!)" reduce iter listSize

    let testArrayPerformance listSize =
        let arrays = List.init listSize (fun i -> [| i.ToString() |])
        let reduce() = arrays |> List.reduce Array.append
        let iter = Array.iter ignore
        time "Array.append" reduce iter listSize

    let testResizeArrayPerformance listSize =
        let resizeArrays = List.init listSize (fun i -> new ResizeArray<string>( [i.ToString()] ) )
        let append (x:ResizeArray<_>) y = x.AddRange(y); x
        let reduce() = resizeArrays |> List.reduce append
        let iter = Seq.iter ignore
        time "ResizeArray.append" reduce iter listSize
```

コードを簡単に説明します：

* ``time`` 関数はreduceと走査の処理時間を計測します。
  ここではコレクションの作成時間については全く測定していません。
  処理を始める前にはGCを実行していますが、特定の型やアルゴリズムによって
  メモリが圧迫されることがあるため、
  GCを実行しておく(あるいは実行しない)ことが重要です。
  コードのパフォーマンスについては [GCの挙動を理解する][link10]
  (Understanding how GC works)が参考になるでしょう。
* ``testListPerformance`` 関数ではコレクション(この場合はリスト)のリスト、
  および ``reduce`` と ``iter`` 関数をセットアップしています。
  その後タイマーを使って ``reduce`` と ``iter`` を実行しています。
* その他の関数も同じことをしていますが、テスト対象がシーケンスや配列、
  (.NETの標準的なリストである)ResizeArrayという違いがあります。
  疑念の余地をなくすために、シーケンスについては標準ライブラリ関数である
  ``Seq.append`` と ``yield!`` の2通りでテストしています。
* ``testResizeArrayPerformance`` ではResizeArrayを使っていて、
  左側のResizeArrayに右側を足しています。
  そうすると左側が変更されて、必要に応じて巨大化されます。
  ここでは挿入時のパフォーマンスを保つことができるように
  効率的な [成長戦略][link11] (growth strategy)が採用されています。

では様々なサイズのリストに対するパフォーマンスを
チェックするためのコードを用意しましょう。
ここでは2000から50000までの間で4000おきのサイズを選択しました。

```fsharp
open Performance

printHeader()

[2000..4000..50000]
|> List.iter testArrayPerformance

[2000..4000..50000]
|> List.iter testResizeArrayPerformance

[2000..4000..50000]
|> List.iter testListPerformance

[2000..4000..50000]
|> List.iter testSeqPerformance_Append

[2000..4000..50000]
|> List.iter testSeqPerformance_Yield
```

是非手元の環境でテストしてみてください。
ここでは詳細な結果は表示しませんが、
結果のチャートだけ掲載しておきます。

![様々なコレクション型に対してモノイド的演算を行う場合のパフォーマンス][img01]

> 訳注：図は [原文のサイト][link01] に掲載されたものを転用しています。

特筆すべきことがいくつかあります：

* シーケンスをベースにした2つの例はスタックオーバーフローでクラッシュしました。
  ``yield!`` は ``Seq.append`` よりも30%高速でしたが、スタックも早く消費しました。
* List.append はスタックを消費し尽くすことがありませんでしたが、
  リストが巨大化するにつれてかなり低速になりました。
* Array.append は高速ですが、リストのサイズに応じてどんどん低速になりました。
* ResizeArrayは最も高速で、しかもリストが巨大化しても
  パフォーマンスの低下がそれほどありませんでした。

クラッシュしなかった3つのコレクション型に対しては
100K(10万)個の要素数でも計測してみました。
結果は以下の通りです：

* List = 150,730 ミリ秒
* Array = 26.062 ミリ秒
* ResizeArray = 33 ミリ秒

この通り、勝者は歴然です。

## 結果の分析

以上のことから何が分かるでしょう？

そもそも聞きたいことが色々とあるでしょう。
実行時のモードはデバッグとリリースどちらだったのか？
最適化は有効にしたのかどうか？
パフォーマンスを改善するために並列化してみたらどうか？
他にも「Yの方が高速なのに何故Xというテクニックを使っているのか？」
といった指摘も当然あるでしょう。

しかし今回は次のようにまとめたいと思います：

* これらの結果から導き出せる結論はありません！

それぞれに状況が異なり、それぞれに異なるアプローチが必要になるのです：

* 小さなデータ集合を対象にする場合には
  おそらくパフォーマンスを気にする必要がありません。
  この場合にはリストがおすすめです。
  パターンマッチや不変性といった機能を無駄に犠牲にする必要はないでしょう。
* リストを足すコードがパフォーマンスのボトルネックになるとはあまり思えません。
  ディスクのI/Oやネットワークの遅延に多くの時間が費やされているのであれば
  リストの足し算を最適化する意味はほとんどありません。
  単語の出現頻度の例を実際に実装する場合、リストを足す処理ではなく、
  ディスクからの読み込みや解析の処理にほとんどの時間がかかるでしょう。
* GoogleやTwitter、Facebookといった規模の会社に勤務しているのであれば
  本当に必要なのは何かしらのアルゴリズムに対するエキスパートを雇うことです。

最適化やパフォーマンスについて言えることは以下の原則だけです：

* ある問題は常に固有のコンテキストに基づいて処理されるべきです。
  つまり処理対象のデータ量であるとか、ハードウェアの種類、
  メモリ量などが重要になります。
  これらが異なればパフォーマンスも違ってきます。
  筆者の環境では解決策になっていたものが、読者の環境でも通用するとは限りません。
  つまりはそういうことです...
* 推測ではなく、常に実測すべきです。
  コードの実行にかかる時間を予想してはいけません。
  プロファイラの使い方を学習すべきです！
  プロファイラの使用法については [こちら][link12] や [こちら][link13] が参考になるでしょう。
* 細部の最適化については用心してください。
  プロファイラはソートにかかる時間のほとんどが
  文字列の比較に費やされているということを報告してくれますが、
  これは必ずしも文字列比較関数を
  最適化すべきだということではありません。
  まずはそれほど大量に比較を行わずに済ませられるよう
  アルゴリズムを改良することを検討してみるとよいでしょう。
  [時期尚早な最適化][link14] (Premature optimization)などについても同様です。

[link01]: http://fsharpforfunandprofit.com/posts/monoids-part3/ "Working with non-monoids"
[link02]: 00.The%20'Understanding%20monoids'%20series.md "「モノイドを理解する」シリーズ"
[link03]: http://fsharpforfunandprofit.com/series/understanding-monoids.html "The 'Understanding monoids' series"
[link04]: 02.Monoids%20in%20practice.md "実践モノイド"
[link05]: http://fsharpforfunandprofit.com/posts/monoids-part2/ "Monoids in practice"
[link06]: http://en.wikipedia.org/wiki/Free_monoid "Free monoid"
[link07]: http://en.wikipedia.org/wiki/Kleene_star "Kleene star"
[link08]: http://scienceblogs.com/goodmath/2008/03/06/monoids-and-computation-syntac/ "Monoids and Computation: Syntactic Monoids"
[link09]: http://opentsdb.net/metrics.html "Metrics"
[link10]: https://www.simple-talk.com/dotnet/.net-framework/5-tips-and-techniques-for-avoiding-automatic-gc-collections/ "5 Tips and Techniques for Avoiding Automatic GC Collections"
[link11]: http://stackoverflow.com/questions/1665298/listt-and-arraylist-default-capacity/1665325#1665325 "List<T> and ArrayList default capacity"
[link12]: http://moiraesoftware.com/blog/2012/07/15/the-lurking-horror/ "The Lurking Horror"
[link13]: http://moiraesoftware.com/blog/2011/12/11/fixing-a-hole/ "Fixing a Hole..."
[link14]: http://programmers.stackexchange.com/a/79954/44643 "What is the best retort to 'premature optimization is the root of all evil'"

[img01]: img/03-01.png
